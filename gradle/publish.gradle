apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url  'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id    'thecodesmith'
            name  'Brian Stewart'
            email 'brian@thecodesmith.com'
        }
    }

    scm {
        connection          'scm:git:https://github.com/thecodesmith/spock-override-extension.git'
        developerConnection 'scm:git:https://github.com/thecodesmith/spock-override-extension.git'
        url                 'https://github.com/thecodesmith/spock-override-extension'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId    project.group
            artifactId project.name
            version    project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode 'name',        'Spock Override Extension'
                root.appendNode 'description', 'Spock extension to allow overriding or ignoring features from inherited specs'
                root.appendNode 'url',         'https://github.com/thecodesmith/spock-override-extension'
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.properties['bintrayUser']   ?: System.getenv('BINTRAY_USER')
    key  = project.properties['bintrayApiKey'] ?: System.getenv('BINTRAY_API_KEY')

    publications = ['maven']

    pkg {
        repo = 'maven'
        name = 'spock-override-extension'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/thecodesmith/spock-override-extension.git'

        version {
            name = project.version
            desc = 'Spock Override Extension'
            released = new Date()
            vcsTag = project.version
        }
    }
}
